(in-package :ghostie)

(defvar *perspective-matrix* nil)
(defvar *ortho-matrix* nil)
(defvar *view-matrix* nil)
(defvar *game-data* nil)

(defclass world ()
  ((physics :accessor world-physics :initform nil)
   (position :accessor world-position :initform '(0 0 -36))
   (level :accessor world-level :initform nil)
   (draw-meta :accessor world-draw-meta :initform nil)))

(defun create-world ()
  (let ((world (make-instance 'world)))
    ;; setup physics
    (let ((space (cpw:make-space :gravity-y -9.8d0)))
      (setf (cp-a:space-sleep-time-threshold (cpw:base-c space)) 3d0)
      (setf (world-physics world) space))
    world))

(defun world-game-cleanup (world)
  (dolist (game-object (level-objects (world-level world)))
    (destroy-game-object game-object))
  (let ((space (world-physics world)))
    (dolist (obj (append (cpw:space-bodies space)
                         (cpw:space-shapes space)
                         (cpw:space-joints space)))
      (cpw:destroy obj))
    (cpw:destroy space)))

(defun game-world-sync (world)
  (let ((level (world-level world)))
    (dolist (game-object (append (level-objects level)
                                 (level-actors level)))
      (sync-game-object-to-physics game-object :render t))))

(defun world-render-cleanup (world)
  (declare (ignore world))
  (free-gl-assets))

(defun step-game-world (world)
  (when *quit* (return-from step-game-world nil))
  (let ((space (world-physics world)))
    (cpw:space-step space)
    (cpw:sync-space-bodies space)
    (dolist (game-object (level-objects (world-level world)))
      (sync-game-object-to-physics game-object))))

(defun free-gl-assets ()
  (loop for (nil obj) on *game-data* by #'cddr do
    (when (subtypep (type-of obj) 'gl-object)
      (free-gl-object obj)))
  (setf *game-data* nil))

(defun init-render (world)
  (free-gl-assets)
  (apply #'gl:clear-color (getf (world-draw-meta world) :background))
  ;; this is the quad we render our FBO texture onto
  (setf (getf *game-data* :quad) (make-gl-object :data '(((-1 -1 0) (1 -1 0) (-1 1 0)) ((1 -1 0) (1 1 0) (-1 1 0)))
                                                 :uv-map #(0 0 1 0 0 1 1 1))))

(defun load-game-assets (world)
  ;; load the current level
  (setf (world-level world) (load-level "physics-test"))
  (init-level-physics-objects world)
  (let ((level-meta (level-meta (world-level world))))
    (let* ((camera (getf level-meta :camera))
           (background (if (getf level-meta :background)
                           (hex-to-rgb (getf level-meta :background) :type 'list)
                           (hex-to-rgb "#262524" :type 'list)))
           (fog-amt (getf (level-meta (world-level world)) :fog-amt))
           (fog-color (if (getf level-meta :fog-color)
                          (hex-to-rgb (getf level-meta :fog-color) :type 'list)
                          background)))
      (setf (getf (world-draw-meta world) :background) background
            (getf (world-draw-meta world) :fog-amt) (if fog-amt fog-amt 0.0)
            (getf (world-draw-meta world) :fog-color) fog-color)
      (when camera
        (setf (world-position *world*) camera))))

  (enqueue (lambda (render-world)
             (dbg :info "Copying game world meta to render world.~%")
             (setf (world-draw-meta render-world) (copy-tree (world-draw-meta world))))
           :render)
  (dbg :info "Finished asset load.~%"))

(defun draw-world (world)
  (when *quit* (return-from draw-world nil))
  (gl:bind-framebuffer-ext :framebuffer (gl-fbo-fbo (getf *render-objs* :fbo1)))
  (gl:clear :color-buffer-bit :depth-buffer)
  (use-shader :main)
  (set-shader-var #'gl:uniformf "fogAmt" (getf (world-draw-meta world) :fog-amt))
  (when (getf (world-draw-meta world) :fog-color)
    (apply #'set-shader-var (append (list #'gl:uniformf "fogColor") (getf (world-draw-meta world) :fog-color))))
  (setf *view-matrix* (apply #'m-translate (world-position world)))
  (set-shader-matrix "cameraToClipMatrix" *perspective-matrix*)
  (when (world-level world)
    (draw-level (world-level world)))
  (gl:bind-framebuffer-ext :framebuffer 0)
  (let ((fbo (getf *render-objs* :fbo1)))
    (gl:clear :color-buffer-bit :depth-buffer-bit)
    (use-shader :dof)
    (set-shader-matrix "cameraToClipMatrix" *ortho-matrix*)
    (set-shader-var #'gl:uniformi "renderTexWidth" 600)
    (set-shader-var #'gl:uniformi "renderTexHeight" 600)
    (gl:active-texture :texture0)
    (gl:bind-texture :texture-2d (gl-fbo-tex fbo))
    (gl:generate-mipmap-ext :texture-2d)
    (set-shader-var #'gl:uniformi "renderTex" 0)
    (gl:active-texture :texture1)
    (gl:bind-texture :texture-2d (gl-fbo-depth fbo))
    (set-shader-var #'gl:uniformi "depthTex" 1))
  (draw-gl-object (getf *game-data* :quad))
  (use-shader 0))

(defun test-gl-funcs ()
  ;(gl:clear-color 1 1 1 1)
  (format t "OpenGL version: ~a~%" (gl:get-string :version))
  (format t "Shader version: ~a~%" (gl:get-string :shading-language-version))
  ;(format t "Extensions: ~a~%" (gl:get-string :extensions))
  (format t "Err: ~a~%" (gl:get-error)))
